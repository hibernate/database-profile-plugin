/*
 * Hibernate, Relational Persistence for Idiomatic Java
 *
 * License: GNU Lesser General Public License (LGPL), version 2.1 or later
 * See the lgpl.txt file in the root directory or http://www.gnu.org/licenses/lgpl-2.1.html
 */

plugins {
	id 'java-gradle-plugin'
	id 'groovy'
	id 'idea'
	id 'eclipse'
	id 'maven'
	id "com.gradle.plugin-publish" version "0.12.0"
}

repositories {
    mavenCentral()
}


// NOTE : plugins published to the Gradle Plugin Portal automatically
// have `gradle.plugin.` prepended to their group name.
//
// It does allow that to be overridden, but then publishing requires
// manual intervention from Gradle team to approve...  not worth the
// hassle
//
// So long story short, not putting "gradle" in groupName here
group = 'org.hibernate.build'
version = '2.0.0-SNAPSHOT'

buildDir = 'target'

sourceSets {
	main {
		// Have the Groovy compiler compile both
		java { srcDirs = [] }
		groovy { srcDirs += 'src/main/java' }
	}
	test {
		resources {
			// add `src/test/java` as a test-resources dir
			configure( srcDir( 'test-projects' ) ) {
				filter {
					include '*'
				}
			}
		}
	}
}

ext {
	pluginId = 'org.hibernate.testing.database-profile'
}

gradlePlugin {
	plugins {
		profilesPlugin {
			id = pluginId
			implementationClass = 'org.hibernate.testing.db.ProfilePlugin'
		}
	}
}

pluginBundle {
	website = 'https://github.com/hibernate/database-profile-plugin/README.adoc'
	vcsUrl = 'https://github.com/hibernate/database-profile-plugin'

	tags = ['database', 'profile', 'jdbc', 'testing', 'hibernate']

	plugins {
		profilesPlugin {
			id = pluginId
			displayName = 'Hibernate database profile testing plugin'
			description = 'Support for testing against multiple databases via profiles'
		}
	}
}

dependencies {
    compile gradleApi()
    compile 'org.apache.ant:ant:1.8.2'

	testCompile 'junit:junit:4.12'
}

task compile
tasks.compile.dependsOn tasks.compileJava, tasks.compileTestJava

idea {
	project {
		languageLevel = '1.6'
	}
	module {
		downloadSources = true
	}
}

task copyResourcesToIntelliJOutFolder(dependsOn: tasks.processTestResources) {
	doLast {
		copy {
			from "$buildDir/resources/test"
			into 'out/test/resources'
		}
	}
}

javadoc {
	options.addStringOption( 'Xdoclint:none', '-quiet' )
}

wrapper {
	// To upgrade the version of gradle used in the wrapper, run:
	//     ./gradlew wrapper --gradle-version NEW_VERSION
	distributionType = Wrapper.DistributionType.ALL
}

